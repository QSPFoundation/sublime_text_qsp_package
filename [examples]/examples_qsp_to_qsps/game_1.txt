# Начало
! BASE
*P 'Укузя 1.2

Привет! Вашему вниманию предоставляется небольшой квест про Укузю. Кто она такая, неизвестно.

Если Вы прошли первую часть игры, то можно загрузиться.




'
ACT 'Начать!':
	gosub 'menu'
	goto 'norka'
END
ACT 'Проект':
	goto 'proect'
END
ACT 'Загрузить':
	goto 'saves'
END
! END BASE
Fcolor = RGB(248,194,240)
	Bcolor = RGB(1,0,0)
if nach = 0:msg 'Совет: Разверните окно на весь экран'&nach=1

-- Начало ---------------------------------

# menu
! Опишем строки меню
$Menu[0]='Посмотреть:glaz'
$Menu[1]='Пощупать руками:shup'
$Menu[2]='Воздействовать предметом:use'
$Menu[3]='Взять в лапки:vzyat'

! Опишем, что на что может 
! действовать.
$voz[1] = 'УкузятаНаОкошко'
$voz[2] = 'УкузятаНаДверка'
$voz[3] = 'Цветочный горшокНаОкошко'
$voz[4] = 'ОсколокНаЗемля'
$voz[5] = 'ВеникНаЗемля'
$voz[6] = 'ВеникНаОсколок'
$voz[7] = 'ОдеялоНаРазбитое окно'
$voz[8] = 'ВеникНаУкузел'
$voz[9] = 'ВеникНаУкузята'
$voz[10] = 'ВеникНаРазбитое окно'
$voz[11] = 'Цветочный горшокНаУкузята'

$COUNTER='Counter'


-- menu ---------------------------------

# counter
!  Если не использовать $z, меню может выпасть еще раз.
if $z!'':
$pr = $z
$z = ''
menu 'Menu'
end
if $selobj!'':
$z = $selobj
$pr = $z
$z = ''
unselect
menu 'Menu'
end
-- counter ---------------------------------

# glaz
$dej = 'glaz'
goto $pr
-- glaz ---------------------------------

# shup
$dej = 'shup'
goto $pr
-- shup ---------------------------------

# use
if $mur='': pl 'Возьми что-нибудь в лапки!'&nl
if $mur!'':
	$u = $mur + 'На'+ $pr
	if arrpos(0,'$voz',$u)!(-1): goto $u
	if arrpos(0,'$voz',$u)=(-1): pl 'Этим ничего не добъешься!'&nl
end

-- use ---------------------------------

# vzyat
$dej = 'vzyat'
goto $pr
-- vzyat ---------------------------------

# norka
$lok = $curloc

if (per = 0)and(buz=0):
	Fcolor = RGB(248,194,240)
	*pl 'Укузя мирно спала в своей уютной норке, и видела какой-то приятный сон.'&*nl
	wait 1000
	msg 'Бум! Бум! Бум!'
	msg 'Хрясь!'
	*pl 'Какой ужас! Что это?'&*nl
	pl 'Укузя проснулась.'&nl
	act 'Оглянуться': Bcolor = RGB(90,71,93)&per = 1 & goto 'norka'
end
if per = 1:
	*pl 'В норке все чисто и красиво, вот только...'&*nl
	if pe = 0: wait 1000
	Fcolor = RGB(233,88,29)
	*pl 'Кошмар! Проломлена крыша! Вот-вот все обвалится!'&*nl
	act 'Срочно разбудить укузят':per = 3 & goto 'norka'
	act 'Разбудить Укузела':per = 3 & pe1 = 1 & goto 'norka'
	act 'Бежать отсюда':per = 2 & goto 'norka'
end
if per = 2:
	*pl 'Укузя в ужасе выбежала на улицу и тут крыша норки с грохотом обрушилась, погребя под собой горячо любимого Укузела и укузят.'&*nl
	wait 1000
	pl 'Горе Укузи не знает границ'&nl
	wait 2000
	*pl 'Нет! Она не могла так поступить! Все было не так, правда?'
	act 'Не так!':per = 1 & pe = 1 & goto 'norka'
	act 'Так!':goto 'end'
end
if per = 3:
	*pl 'В норке все чисто и красиво, вот только...'&*nl
	*pl 'Кошмар! Проломлена крыша! Вот-вот все обвалится!'&*nl
	if pe1 = 0:
		pl 'Проснувшись, укузята немедленно стали плакать при виде испуганной матери'&nl
		wait 1000
	end
	pl 'От громких криков Укузел проснулся'&nl
	msg 'Укузя! Что тут у вас происходит?!'
	act 'Милый! Какой-то ужасный грохот!':
		msg 'Милый! Какой-то ужасный грохот!'
		pl 'Укузел стал прислушиваться'&nl
		wait 1000
		pl 'Крыша рухнула' 
		wait 2000 & goto 'end'
	end
	act 'Ах! Мои укузята! Мы погибли!':
		msg 'Ах! Мои укузята! Мы погибли!'
		pl 'Укузел вытаращился на Укузю'&nl
		wait 1000
		pl 'Крыша рухнула' &nl
		wait 2000 & goto 'end'
	end
	act 'Посмотри наверх! Крыша!':
		msg 'Посмотри наверх! Крыша!'
		pl 'Укузел мигом сориентировался в обстановке, вскочил и уперся плечами в низкий потолок'&nl
		wait 1000
		per = 4 & goto 'norka'
	end
end
if per = 4:
	Bcolor = RGB(90,71,93)
	*pl 'Посреди норки стоит слегка побагровевший Укузел и подпирает плечами потолок'& *nl
	act 'Укузенька, как ты?!?': 
		msg 'Укузенька, как ты?'
		msg 'Сделай что-нибудь, я так вечно стоять не смогу!'
	end
	if pe5 = 1: 
		act 'Я пристроила детей к Бузюле': 
			msg 'Я пристроила детей к Бузюле'
			msg 'Это хорошо, но я не смогу так долго стоять! Позови Фиреля!'
		end
	end
	act 'Выбежать из норки':
		if (pe5 = 0) and (no obj 'Укузята'): pl 'Укузя не может оставить детей!'&nl
		if (obj 'Укузята') or (pe5 = 1): pl 'Укузя поспешно выскочила из норки' &nl & goto 'polyana'
	end
	if pe2 = 1: msg 'О чем ты думаешь, Кузиния! Беги за помощью!'& pe2 = 0
	if pe2 = 2: msg 'Укузька! Ты в своем уме! Нашла время подметать! А ну дуй отсюда за помощью!' &pe2 = 0
	if pe5 = 0 and no obj 'Укузята': act 'Укузята': $z = 'Укузята'
	if no obj 'Веник': act 'Веник': $z = 'Веник'
end
if per = 5: 
	Bcolor = RGB(145,88,19)
	FColor = RGB(248,194,240)
	*pl 'В уютной Укузиной норке царит полный разгром. Кровать Укузи и Укузела поставлена на попа и подпирает потолок вместо Укузела. Фирель с Укузелом пытаются починить крышу.'
	pe7 = 1
	act 'Выйти из норки': goto 'polyana'
	act 'Говорить с Укузелом':
		msg 'Милый! С тобой все в порядке?'
		msg 'В порядке, в порядке!'
		msg 'Такой ужас! Такой ужас!'
		msg 'Шла бы ты лучше к Бузюле, Укузенька! Без тебя тут разберемся!'
	end
	act 'Говорить с Фирелем':
		msg 'Скажите, можно это как-то починить?'
		msg 'Иди-иди, Укузя, не женское это дело.'
	end
	if no obj 'Веник': act 'Веник': $z = 'Веник'
	if pe2 = 1: pe2 = 0
	if pe2 = 2: msg 'Потом подметешь, когда починим! Сейчас все равно мусора набросаем!' &pe2 = 0
end
if buz!0:
	Bcolor = RGB(145,88,19)
	*pl 'Крыша в Укузиной норке кое-как укреплена, но кровать по-прежнему подпирает потолок на всякий случай.'
	if nork=0: 
		*pl 'На кроватке старшего Укузенка, сложившись вчетверо, спит Укузел.'
		act 'Укузел':$z= 'Укузел'
	end
	if nork=2:
		*pl 'В норке никого нет.'
	end
	if (no obj 'Веник')and(venik=0):act 'Веник':$z='Веник'
	act 'Выйти из норки':goto 'polyana'
end

if $mur!'' : *nl & *nl & *pl 'В лапках у Укузи '+ $mur


-- norka ---------------------------------

# end
if per = 2:
	*pl 'Увы, оставшись без дорогих ей существ, Укузя впала в такую глубокую депрессию, что больше ничего и никогда не стала делать.'
end
if per = 3:
	*pl 'Нда... Если бы Укузя лучше соображала, все могло быть не так...'
end
*nl&*nl&*nl& *pl '          КОНЕЦ'
-- end ---------------------------------

# Укузята
if $dej = 'glaz': 
	if bu1<4: pl 'Укузя торопливо пересчитывает детей. Все на месте.'&nl
	if bu1>3: pl 'Милые детки! Даже представить страшно, какое несчастье могло случиться вчера!'&nl
end
if $dej = 'shup': 
	pl 'Надо проверить, все ли целы! Ах! У младшего Укузенка заноза!'&nl
	if bu1>3: 
		msg 'Давай-ка, вынем эту занозу!'
		msg 'Нет! Я боюсь! Не надо!'
		msg 'Нет надо! А то она воспалится!'
		msg 'Тогда пойдем к бабушке Омазе! А тебе я не дамся!'
		pl 'Такой маленький и такой мнительный!'&nl
		uk=1
	end
end
if $dej = 'vzyat':
	if bu1<4:
		$mur = $pr
		if no obj $pr: 
			pl 'В первую очередь, детей надо увести из опасного места!'&nl
			add obj $pr
		end
	end
	if uk=1:
		if (bu1>3)and(nork=0):
			msg 'Разбужу папу и вернусь! Тогда и пойдем!'
			msg 'Хорошо!'
		end
		if (bu1>3)and(nork=2):
			$mur='Укузенок'
			if no obj 'Укузенок': 
				add obj 'Укузенок'
				msg 'Пойдем-ка к бабушке Омазе!'
				pl 'Старший Укузенок вприпрыжку убегает.'&nl
			end
		end
	end
	if uk=0: pl 'Да пусть играют! Зачем брать их с собой?'&nl
end
goto $lok
-- Укузята ---------------------------------

# Веник
if $dej = 'glaz': 
	if (no obj 'Веник')and ($lok!'busulya') : pl 'В углу норки стоит веник'&nl
	if (obj 'Веник'): pl 'Симпатичный новенький веничек. Укузел сделал его прошлым летом'&nl 
	if (no obj 'Веник')and($lok='busulya'):pl 'Ну я вчера и перенервничала! Веник из дома зачем-то притащила!'&nl
end
if $dej = 'shup': 
	if ($lok = norka)and(per!0): pe2 = 2


end
if $dej = 'vzyat':
	$mur = $pr
	pl 'Укузя взяла веник'&nl
	if $lok='norka':pe2 = 1
	if $lok='busulya':venik=0
	if no obj $pr: add obj $pr
end
goto $lok
-- Веник ---------------------------------

# polyana
$lok = $curloc
*pl 'Укузина норка - на красивой полянке. Они с Укузелом обошли пол-леса, прежде чем нашли полянку, которая устроила бы их обоих.'&*nl
if (per1 = 0)and(buz=0):
	Bcolor = RGB(1,0,0)
	act 'Посмотреть на норку': per1 = 1 & goto 'polyana'
end
if per1 = 1:
	*pl 'Несмотря на темноту видно, что на Укузину норку будто бы...'&*nl
	if pe3 = 0:
		wait 700
		*pl 'будто бы...'&*nl
		wait 700
		pe3 = 1
	end
	*pl 'Кто-то наступил!'&*nl	
	if pe4 = 1: act 'Идти к ручью':goto 'ruchej'
	act 'Следы': $z = 'Следы'
end
 
act 'В норку Укузи': goto 'norka'
if buz=0: 
	act 'Бежать к Бузюле': goto 'dom_busuli'
	act 'Бежать к Фирелю': goto 'dom_firelya'
end
if buz!0:
	Bcolor = RGB(175,106,22)
	act 'Идти к Бузюле':goto 'dom_busuli'
	act 'Идти к Фирелю':goto 'dom_firelya'
	act 'Идти к лесному ручью':goto 'ruchej'
end
if pol = 0:
	act 'Посмотреть на норку':pol=1&goto $lok
end
if pol=1:
	pl 'Фирель с Укузелом трудились всю ночь, но все равно еще заметно, что Укузина норка была раздавлена чьей-то огромной ногой.'&nl
	pl 'Следов того, кто это сделал, уже не видно.'&nl
	pol=0
	goto $lok
end
if $mur!'' : *nl & *nl & *pl 'В лапках у Укузи '+ $mur
-- polyana ---------------------------------

# Следы
if $dej = 'glaz': 
	pl 'На поляне примята трава. Видно, что здесь прошел кто-то очень большой. Как неудачно, что его путь пришелся прямо по Укузиной норке!'
	pl 'Следы ведут в сторону лесного ручья.'&nl
	pe4 = 1
end
if $dej = 'shup': 
	pl 'Укузя осторожно ступает на ближайший след. Да в него поместится еще три Укузи!'&nl
end
if $dej = 'vzyat':
	pl 'Как бы не хотелось Укузе унести след с собой в качестве вещественного доказательства, ничего не получится.'&nl
end
goto $lok
-- Следы ---------------------------------

# dom_busuli
$lok = $curloc
if (per3 = 0)and(buz=0):
	Bcolor = RGB(1,0,0)
	*pl 'Укузя подбежала к норке своей хорошей подруги Бузюли.'
	*pl 'Дверь закрыта, Бузюля спит.'&*nl
	act 'Стучать в дверь': 
		msg 'Стук-стук-стук!'
		pl 'Увы, Бузюля всегда отличалась хорошим сном!'&nl
	end
	act 'Кричать': 
		msg 'Бузю-у-у-ля-а-а-а!'
		pl 'Да ее из пушки не разбудишь!'&nl
		pe5 = 1
		goto 'dom_busuli'
	end
	if pe5 = 1: act 'Поглядеть по сторонам': per3 = 1 & goto 'dom_busuli'
end
if per3 = 1:
	Bcolor = RGB(1,0,0)
	*pl 'У Бузюли обычная норка. Земляной холмик, поросший травой, маленькая дверка, круглое окошечко...'&*nl 
	act 'Дверка': $z = 'Дверка'
	act 'Окошко': $z = 'Окошко'
	if pe6 = 1: 
		act 'Подбежать к укузенку':
			del act 'Подбежать к укузенку'
			msg 'Узенька, больно?'
			msg 'Да-а-а!'
			pl 'Укузенок утешен' & nl
			msg 'Я споткнулся об этот цветочный горшок! Тетя Бузюля будет ругаться, да?'&*nl
			pe6 = 2 & goto 'dom_busuli'
		end
	end
	if pe6 = 2:
		if no obj 'Цветочный горшок': act 'Цветочный горшок': $z = 'Цветочный горшок'
	end
end
if per3 = 2:
	Bcolor = RGB(38,51,100)
	*pl 'В разбитом окна показывается заспанная перепуганная Бузюля'
	msg 'А? Что? Кого?... Что происходит?!'
	act 'Бузька, это я!':
		msg 'Укузя? Да ты что, с ума сошла?! Мое окно!'
		msg 'Бузюля, не бузи! У моей норки обвалилась крыша! Последи за укузятами, я побегу выручать Укузела!'
		msg 'Укузя! Как же так?...'
		msg 'Некогда, соседка!'
		del act 'Бузька, это я!'
		del obj 'Укузята'
		pe6 = 1& per = 4 & per3 = 3 & per1 = 0
	end
end
if per3 = 3 and pe7 = 0:
	pl 'Укузя уже отвела детей к Бузюле, надо выручать Укузела!'&nl& goto 'polyana'
end
if per3 = 3 and pe7 = 1:
	Bcolor = RGB(38,51,100)
	*pl 'У Бузюли обычная норка. Земляной холмик, поросший травой, маленькая дверка, круглое окошечко...'&*nl 
	act 'Дверка': $z = 'Дверка'
	act 'Разбитое окошко': $z = 'Окошко'
end
if per3 = 4:
	*pl 'Укузя в комнате у Бузюли. Бузюля с укузятами сидят за столом и пьют теплое молоко.'&*nl
	msg 'Ну рассказывай, рассказывай, что там у вас случилось?'
	msg 'Как тут мои укузяточки?'
	msg 'Хорошо, мама!'
	msg 'А папа уже не держится за потолок?'
	msg 'Нет, папа с дядей Фирелем чинят крышу!'
	msg 'Почему у вас вдруг обвалилась крыша, ты знаешь?'
	pl 'Укузя садится за стол'&nl
	if per1 = 0: msg 'Не знаю, ни с того, ни с сего... Надо будет посмотреть внимательно.'
	if per1>0:
		msg 'На нашу норку наступил кто-то очень большой!'
		msg 'Правда? Как это удивительно!'
	end
	msg 'Как бы то ни было, укузятам пора в постель.'
	*pl 'Укузя и Бузюля уложили укузят спать, а сами еще долго обсуждали удивительное ночное проишествие.'&*nl
	per3 = 5
	wait 1000
	act 'Наступило утро': cha=2 & goto 'zagruz'
end
if buz!0:
	Bcolor = RGB(175,106,22)
	Fcolor = RGB(254,235,5)
	act 'В норку Бузюли': goto 'busulya'
	*pl 'У Бузюли обычная норка. Земляной холмик, поросший травой, маленькая дверка, круглое окошечко...'&*nl
	if (bu1>3)and(no obj 'Укузенок'):
		*pl 'Возле норки играют укузята.'
		act 'Укузята':$z='Укузята'
	end
end
    


if per3<4: act 'На поляну': goto 'polyana'

if $mur!'' : *nl & *nl & *pl 'В лапках у Укузи '+ $mur

-- dom_busuli ---------------------------------

# ruchej
$lok = $curloc
if (per4 < 1)and(buz=0):
	pl 'Укузе надо отвести детей в безопасное место и выручить Укузела, остальное подождет!'&nl
	goto 'polyana'
end
if (per3 < 6)and(buz=0):
	Pl 'Пожалуй, лучше отложить это до утра. Все равно в темноте ничего не разглядишь'&nl
	goto 'polyana'
end
*pl 'Лесной ручеек. Здесь Укузя берет воду для хозяйства.'
*pl 'Через ручеек перекинуто бревно, которое служит мостом на другой берег.'
if ruch = 0:
	Bcolor = RGB(120,120,16)
	*pl 'На берегу сидит младший Укузенок и шлепает палочкой по воде.'
	act 'Ругаться':
		msg 'Здрасте! Ты почему это ушел, никому ничего не сказав?'
		msg 'Мама! А я хотел выяснить, что у нас случилось с крышей!'
		msg 'Мог бы меня разбудить, я же волнуюсь, такие жуткие вещи, а ты где-то бродишь!'
		msg 'А я знаю, что вчера произошло!'
		msg 'Пойдем-ка к Бузюле, пора завтракать!'
		pl 'Укузя хватает за руку Укузенка и ведет его к Бузюле.'&nl
		add obj 'Укузенок'
		wait 1000
		Bcolor = RGB(175,106,22)
		pl 'Укузя выходит на поляну.'&nl
		msg 'Ну мама, ну я знаю, ну хочешь, скажу!'
		msg 'Ну что?'
		msg 'На нас кто-то наступил! Вот!'
		pl 'Укузя немного раздумывает.'&nl
		wait 1000
		pl 'Нет, не стоит его пугать!'&nl
		msg 'Ну что еще за глупости! Таких больших зверей не бывает.'
		msg 'А вот и бывает, вот и бывает! Бабушка Омаза рассказывала!'
		msg 'Бабушка Омаза знает много сказок, а по-настоящему такого не бывает!'
		msg 'А вот и не сказок, вот и не сказок! Она говорит, что сама видела огромного Умарзла!'
		pl 'Они подходят к дому Бузюли.'
		msg 'Бабушка Омаза сказала так, чтобы ты поверил в Умарзлов, никого она не видела!'
		msg 'Она не мне говорила, а своей подруге, я случайно услышал, когда ходил к ней лечить левую лапку, помнишь?'
		msg 'Помню-помню, как ты упал с дерева Фиреля, чтобы не смел больше забираться к нему без взрослых!'
		msg 'Ну ма-а-ма-а!'
		msg 'И чтобы никаких больше Умарзлов, а то я рассержусь!'
		pl 'Укузенок обиженно замолкает.'&nl
		pl 'Укузя с Укузенком входят в норку Бузюли.'&nl
		del obj 'Укузенок'
		bu1=1
		ruch=1
		goto 'busulya'
	end	
end	
if ruch=1:
	act 'Перейти через ручей':goto 'dor_omaz'
end
act 'На поляну':goto 'polyana'
if $mur!'' : *nl & *nl & *pl 'В лапках у Укузи '+ $mur
-- ruchej ---------------------------------

# Дверка
if $dej = 'glaz': 
	pl 'Маленькая дверка. У Укузи дверка повыше, чтобы Укузел не стукался головой.'&nl
end
if $dej = 'shup': 
msg 'Стук-стук-стук!'
	if per3<3: pl 'Увы, Бузюля всегда отличалась хорошим сном!'&nl
	if per3 = 3: msg 'Укузя, это ты? Заходи!'& per3 = 4 	
end
if $dej = 'vzyat':
	pl 'Сорвать дверь с петель Укузе не под силу. А жаль!'&nl
end
goto $lok
-- Дверка ---------------------------------

# Окошко
if $dej = 'glaz': 
	pl 'Укузя заглядывает в окошко. В темноте ничего не разглядеть.'&nl
end
if $dej = 'shup': 
msg 'Стук-стук-стук!'
	if per3<3: pl 'Увы, Бузюля всегда отличалась хорошим сном!'&nl
	if per3 = 3: Bcolor = RGB(145,88,19)& per3 = 4&msg 'Укузя, это ты? Заходи!' 	
end
if $dej = 'vzyat':
	pl 'Выставить раму?'&nl
end
goto $lok

-- Окошко ---------------------------------

# УкузятаНаОкошко
msg 'Ну-ка, детки, может, у вас получится разбудить тетю Бузюлю!'&*nl
pl 'Укузята бегут к окну, младший спотыкается, падает и начинает плакать. Старший останавливается.'&nl
pe6 = 1
goto $lok
-- УкузятаНаОкошко ---------------------------------

# УкузятаНаДверка
msg 'Ну-ка, детки, может, у вас получится разбудить тетю Бузюлю!'&*nl
pl 'Укузята подбегают к двери и начинают изо всех сил стучать'&nl
msg 'Стук-стук-стук!'
pl 'Увы, Бузюля всегда отличалась хорошим сном!'&nl
goto $lok
-- УкузятаНаДверка ---------------------------------

# Цветочный горшок
if $dej = 'glaz': 
	pl 'Что за дурацкая манера у Бузюли, выращивать уличные цветы в горшках! Нет бы как все, на клумбах!'&nl
end
if $dej = 'shup': 
	pl 'Цветок сломан... А горшок вполне увесистый!'&nl
end
if $dej = 'vzyat':
	$mur = $pr
	if no obj 'Цветочный горшок': pl 'Возьму-ка я этот горшок, все равно цветок погиб.'&nl
	if no obj $pr: add obj $pr 
end
goto $lok
-- Цветочный горшок ---------------------------------

# Цветочный горшокНаОкошко
msg 'Ох, как будет ругаться тетя Бузюля!'
pl 'Укузя запускает горшком в окно'&nl
del obj 'Цветочный горшок'
$mur = ''
wait 1000
msg 'Дзинь-дзы-ынь-нь! нь! нь!'
per3 = 2
goto $lok

-- Цветочный горшокНаОкошко ---------------------------------

# dom_firelya
$lok = $curloc
*pl 'Фирель живет не в норке, а на дереве. По деревьям Укузя лазить не умеет, но у Фиреля для гостей придуман отличный подъемник. Фирель вообще мастер на все руки.'&*nl
if (per4 = 0)and(buz=0):
	Bcolor = RGB(1,0,0)
	act 'Подняться на подъемнике':
		if obj 'Укузята': pl 'Подъемник расчитан на одного человека, а бросать укузят одних ночью Укузя не решается'&nl
		if no obj 'Укузята':
			Bcolor = RGB(43,64,4)
			msg 'Скрип-скрип-скрип'
			del act 'Подняться на подъемнике'
			act 'Разбудить Фиреля': 
				del act 'Разбудить Фиреля'
				msg 'Фирель, Фирель! Проснись!'
				pl 'Фирель проснулся'&nl
				msg 'Укузя! Что случилось?'
				msg 'Ох, Фирель, в нашей норке обрушилась крыша! Мой бедный Укузел держит потолок на плечах, но он, того и гляди, рухнет!'
				msg 'Вот несчастье! А укузята?'
				msg 'Укузята в порядке, я отвела их к Бузюле.'
				msg 'Ну ничего, ничего, Укузя, я уже бегу!!'
				Fcolor = RGB(248,194,240)
				pl 'Фирель поспешно слезает с дерева и убегает'&nl& per = 5 & per4 = 1
			end
		end
	end
end
if per4 = 1:
	*pl 'Фиреля нет дома'&*nl
end
if buz!0:
	if fir=0:
		act 'Подняться на подъемнике':
			if (bu1<5)and(nork!2):
				pl 'Не надо будить Фиреля! В конце концов он всю ночь чинил Укузе крышу!'&nl
			end
			if (bu1=5)or(nork=2):
				msg 'Скрип-скрип-скрип!'
				Bcolor = RGB(120,120,16)
				fir=1
				goto $lok
			end
		end
	end
	if fir=1:
		if nork=2: pl 'Хм! Никого нет! Интересно, куда они ушли?'&nl
		if nork=0:	
			if fi=1: msg 'Укузя? Скажи мужу, пусть ко мне зайдет!'
			if fi=0:
				pl 'Фирель уже не спит!'&nl
				msg 'Привет, Укузя!'
				act 'Поздороваться':
					msg 'Здравствуйте, Фирель!'
					msg 'Ну и работку ты мне задала! Пару дней поживите с укузятами у Бузюли!'
					msg 'Да, конечно...'
					msg 'Работы много, ну да мы с Укузелом справимся. Если, конечно, еще какой-нибудь Умарзел вам на крышу не наступит!'
					msg 'Неужели Вы тоже верите в сказки Омазы!'
					msg 'Сказки? Омаза знает, что говорит!'	
				end	
				act 'Попросить вставить стекло':
					msg 'Знаете, я вчера не могла добудиться Бузюлю...'
					msg 'Да уж! Если Бузюля спит, ее трудно поднять!'
					msg 'Да... И мне пришлось разбить ей окно!'
					msg 'Ловко! Это ее разбудило?'
					msg 'Разбудило! Но ее окно...'
					msg 'Э, нет! Что-нибудь одно! Закончу с твоей крышей, а потом и разбитым окном займусь.'
					fi=1
				end
			end 
		end
		act 'Спуститься':
			msg 'Скрип-скрип-скрип!'
			fir = 0
			Bcolor = RGB(175,106,22)
			goto $lok
		end
	end
end
	
if fir=0:  act 'На поляну': goto 'Polyana'

				
		
-- dom_firelya ---------------------------------

# proect
! BASE
*P 'Вообще-то, планируется все это озвучить и даже нарисовать. Но сначала стоит дописать все до конца. Это - просто завязка сюжета, а что там будет дальше, я пока не знаю. Отзывы и идеи принимаются по адресу: t_tanusha@yahoo.com. '
ACT 'Обратно':
	goto 'Начало'
END
! END BASE
-- proect ---------------------------------

# saves
! BASE
*P 'Если Вы проходили первую часть, ответьте на контрольные вопросы!
'
ACT 'Вопрос1':
	$sav = input('Какой предмет помог Укузе разбудить Бузюлю?')
	if (lcase($sav)= 'цветочный горшок')or(lcase($sav)='горшок'):
	  sa =1
	  msg 'Правильно! Ответьте на второй вопрос!'
	end
	if sa!1:
	  msg 'Увы и ах! Все было не так!'
	  msg 'Начните сначала'
	end
	
END
ACT 'Вопрос2':
	if sa = 1:
	  cha=2
	  $sav2=input('Что осталось у в лапках у Бузюли в самом конце?')
	  if lcase($sav2)='веник': add obj 'Веник'
	  if no obj 'Веник':
	    msg ($sav2 + ' - неправильный ответ, поэтому после загрузки у Вас ничего не останется!')
	    msg 'Не печальтесь, это не принципиально.'
	  end
	  msg 'Ну поехали!'
	  goto 'zagruz'
	end
	if sa!1: msg 'Сначала первый вопрос!'  
	  
END
ACT 'Вернуться':
	goto 'Начало'
END
! END BASE
-- saves ---------------------------------

# zagruz

if cha=2: 
  killvar
  cls
  gosub 'menu'
  if obj 'Веник': killobj & venik=1 
  goto 'busulya'
end



-- zagruz ---------------------------------

# busulya
$lok =$curloc

if buz = 0:
	Bcolor = RGB(145,88,19)
	Fcolor = RGB(52,29,1)
    if bu = 0:
    	pl 'Солнечные лучи разбудили Укузю.'&nl
    	msg 'Где это я?'
    end
    *pl 'Бузюлина норка. На Бузюлиной кровати - старший Укузенок. Бузюля спит на диванчике. Укузю уложили на раскладушке.'	
    *pl 'Младшего Укузенка нигде не видно.'
    act 'Посмотреть по сторонам': 
    	buz = 1
    	goto $lok
    end
end    
if bu1 < 1:    
    act 'Разбудить Бузюлю':
        pl 'Укузя тормошит Бузюлю.'&nl
        msg 'Бузюля-я!'
        pl 'Бузюля бормочет что-то невразумительное и переворачивается на другой бок.'&nl
        wait 1000
        pl 'Нда-а! Это не тот кадр!'&nl
    end
end
if bu = 0:       
    act 'Разбудить старшего Укузенка':
        pl 'Укузя тормошит старшего Укузенка'&nl
        msg 'А? Мама!'
        pl 'Укузенок начинает тереть глаза и потягиваться.'&nl
        bu = 1 & goto $lok
    end
end
if bu = 1:        
    act 'Где твой брат?':
        msg 'С добрым утром! Где твой брат?'
        pl 'Старший Укузенок удивленно оглядывается'&nl
        msg 'А почему мы у тети Бузюли?'
        msg 'Ты разве не помнишь, что было вчера вечером?'
        pl 'Укузенок постепенно просыпается'&nl
        msg 'А, ну да! Мы пришли ночью...'
        msg 'У нас что-то с потолком...'
        msg 'Так ты не знаешь, где твой брат?'
        msg 'Не знаю, я же спал!'
        bu=2 & goto $lok
    end
end 
if buz = 1:
	Bcolor = RGB(145,88,19)
	Fcolor = RGB(52,29,1)
	*pl 'В Бузюлиной норке ужасный беспорядок. Разбитое окно кое-как затянуто одеялом, на полу валяется земля, осколки стекла и цветочного горшка.'
	if venik = 1: 
		*pl 'Возле Укузиной раскладушки лежит Укузин веник. Вчера на нервной почве Укузя зачем-то притащила его из дома.'              
    	if no obj 'Веник': act 'Веник': $z = 'Веник'
    end
    act 'Осколки': $z= 'Осколок'&osk=1
    act 'Земля': $z= 'Земля'
end
if (buz=1)or(buz=2)or(buz=3):
    act 'Разбитое окно':$z= 'Разбитое окно'
    act 'Выйти из норки':
     	if no obj 'Одеяло':
    		if (bu=1)or(bu=2): msg 'Мама, а куда ты уходишь?'
    		if bu=1:
    			msg 'С добрым утром! Где твой брат?'
       			pl 'Старший Укузенок удивленно оглядывается'&nl
       			msg 'А почему мы у тети Бузюли?'
       			msg 'Ты разве не помнишь, что было вчера вечером?'
       			pl 'Укузенок постепенно просыпается'&nl
       			msg 'А, ну да! Мы пришли ночью...'
       			msg 'У нас что-то с потолком...'
       			msg 'Так ты не знаешь, где твой брат?'
       			msg 'Не знаю, я же спал!'
       			bu=2 & goto $lok
    		end
    		if bu=2:
    			msg 'Пойду, поищу твоего братика.'
   				pl 'Укузенок испугался!'&nl
    			msg 'А как же я?'
    			msg 'Он еще маленький, вдруг с ним что-нибудь случится!'
    			msg 'А со мной ничего не случится?'
    			msg 'Ну ты же у нас уже взрослый! Я скоро вернусь, можешь еще поспать.'
    			pl 'Укузенок колеблется.'&nl
    			msg 'А крыша не обвалится, как вчера?'
    			msg 'Нет, конечно! И тетя Бузюля за тобой присмотрит.'
    			pl 'Бузюля переворачивается на другой бок.'&nl
    			wait 1500
    			pl 'Укузенок неуверен'&nl
    			msg 'Ну если только тетя Бузюля...'
    			msg 'Будь умницей!'
    		end
    		bu = 3 & goto 'dom_busuli'
    	end
    	if obj 'Одеяло': 
    		pl 'Не стоит разгуливать по лесу с одеялом!'&nl
    		pl 'Укузя кладет одеяло на свою раскладушку.'&nl
    		del obj 'Одеяло'
    		$mur=''
    		goto $lok
    	end
    end 
end
if buz=2:
	Bcolor = RGB(145,88,19)
	Fcolor = RGB(52,29,1)
	*pl 'В Бузюлиной норке неуютно, потому что разбитое окно кое-как затянуто одеялом.'
end
if buz=3:
	Bcolor = RGB(145,88,19)
	Fcolor = RGB(254,235,5)
	*pl 'В Бузюлиной норке неуютно, потому что окно разбито. Зато светло!'
	if no obj 'Одеяло':
		act 'Одеяло':$z='Одеяло'
	end
end
if bu1=1:
	pl 'Оба-на! Бузюля уже проснулась!'&nl
	wait 1000
	msg 'А! Вот и вы! Завтрак скоро будет готов!'
	msg 'Это хорошо, я тебе помогу!'
	msg 'Укузел тоже придет?'
	if nor=0: msg 'Я с ним не разговаривала! Наверное, он захочет поспать подольше.'
	if nor=1: msg 'Я не стала его будить, пусть поспит подольше.'
	wait 1000
	if buz<3: bu1=2
	if buz=3: bu1=3
end
if bu1=2:	 
	if bu2=0:
		if buz=1:
			msg 'Укузя, тут бы надо убраться. Мой веник, как назло, совсем облез. Из-за стекольных осколков мыть пол опасно...'
			if (obj 'Веник')or(venik=1): 
				msg 'Да вот же, у меня есть веник, я сейчас все подмету!'
				msg 'Спасибо!'	
			end
			if (no obj 'Веник')and(venik=0):
				msg 'Дома у меня есть веник! Я сейчас принесу!'
				msg ' Будь добра!'
			end
			bu2=1
		end
	end
	if (bu2=2)or(bu2=0):
		if buz=2: 
			msg 'Темно тут как-то! Укузя, ты одеяло с окна не снимешь, пока я на стол подам?'
		end
		bu2=3
	end
	if bu2=4: bu1=3
end	
if bu1=3:
	msg 'Все за стол!'
	wait 1000
	pl 'После завтрака Укузята убежали играть на улицу.'&nl
	bu1=4
end
if bu1=4:
	act 'Говорить с Бузюлей':
		msg 'Бузя, я попрошу Фиреля, чтобы он вставил новое стекло.'
		msg 'Да, попроси, пожалуйста! С разбитым окном жить невозможно.'
		bu1=5
	end
end
if nork=1:
	wait 1000
	msg 'Ням-ням-ням-ням!'
	msg 'Спасибо, девчонки! Пойду к Фирелю, надо приводить нашу норку в порядок!'
	pl 'Укузел уходит.'&nl
	nork=2
end    	
if $mur!'' : *nl & *nl & *pl 'В лапках у Укузи '+ $mur

-- busulya ---------------------------------

# Осколок
if $dej = 'glaz': 
	if osk=1: pl 'Ой! Какая грязь! Вчера в темноте Бузюля убедила Укузю оставить уборку на завтра, но сегодня убраться просто необходимо!'&nl
	if osk=0: pl 'Осколок цветочного горшка.'&nl
end
if $dej = 'shup': 
	if osk=1: pl 'Так я порежусь! Не хочу руками! Мне бы веник!'&nl
	if osk=0: pl 'Хорошо, что это не осколок стекла, а то бы Укузя порезалась!'& nl
end
if $dej = 'vzyat':
	if obj 'Осколок':pl 'Укузя берет осколок'&nl
	if no obj 'Осколок':
		pl 'Укузя берет себе осколок цветочного горшка.'&nl
		add obj 'Осколок'
		osk=0
	end
	$mur = 'Осколок'
end
goto $lok

-- Осколок ---------------------------------

# Земля
if $dej = 'glaz': 
	pl 'Ой! Какая грязь! Вчера в темноте Бузюля убедила Укузю оставить уборку на завтра, но сегодня убраться просто необходимо!'&nl
end
if $dej = 'shup': 
	pl 'У Укузи совершенно нет желания прикасаться к этому лапками!'&nl
end
if $dej = 'vzyat':
	pl 'Собрать лапками всю эту землю? И куда ее девать?'&nl
end
goto $lok

-- Земля ---------------------------------

# Разбитое окно
if $dej = 'glaz': 
	pl 'Да! Как неудобно получилось! Обязательно надо будет попросить Фиреля вставить новое стекло!'&nl
end
if $dej = 'shup': 
	if buz=1: pl 'К окну не подойти, весь пол в земле и осколках!'&nl
	if buz!1: pl 'Нет уж! Укузя может порезаться!'&nl
end
if $dej = 'vzyat':
	if buz=1:
		pl 'К окну не подойти, весь пол в земле и осколках!'&nl
	end
	if buz>2: pl 'Не надо вынимать из окна стекло! Лучше пусть это сделает Фирель!'&nl
	if buz=2:
		pl 'Укузя осторожно снимает с окна одеяло.'&nl
		if bu=0: 
			pl 'Солнечный луч врывается через окно и будит Укузенка.'&nl
			bu = 1
		end
		buz=3
		add obj 'Одеяло'
		$mur = 'Одеяло'
		if bu1!0:bu2=bu2+1
	end
end
goto $lok
-- Разбитое окно ---------------------------------

# ОсколокНаЗемля
pl 'Забавная идея! Этак всю жизнь придется выскребать и выносить по одной крупинке!'&nl
goto $lok
-- ОсколокНаЗемля ---------------------------------

# ВеникНаЗемля
pl 'Укузя хватает веник и начитает подметать пол.' 
pl 'Она открывает дверь и выносит мусор на улицу.'&nl
if bu=0: 
	pl 'Солнечный луч врывается через открытую дверь и будит Укузенка.'&nl
	bu = 1
end
buz=2
if bu1!0: bu2=2
goto $lok
-- ВеникНаЗемля ---------------------------------

# ВеникНаОсколок
if osk=0: pl 'Этим ничего не добъешься!'&nl
if osk=1:
	pl 'Укузя хватает веник и начитает подметать пол.' 
	pl 'Она открывает дверь и выносит мусор на улицу.'&nl
	if bu=0: 
		pl 'Солнечный луч врывается через открытую дверь и будит Укузенка.'&nl
		bu = 1
	end
	buz=2
	osk=0
	if bu1!0: bu2=2
end

goto $lok
-- ВеникНаОсколок ---------------------------------

# Одеяло
if $dej = 'glaz': 
	pl 'Одеяло. Скорее плед. В клеточку!'&nl
end
if $dej = 'shup': 
	pl 'Одеяло довольно тонкое. Хорошо, что сейчас лето и ночь была теплой!'&nl
end
if $dej = 'vzyat':
	pl 'Укузя взяла одеяло.'&nl
	if no obj 'Одеяло':
		add obj 'Одеяло'
	end
	$mur= 'Одеяло'
	bu2=4
end
goto $lok

-- Одеяло ---------------------------------

# ОдеялоНаРазбитое окно
pl 'Бузюля завешивает окно одеялом.'&nl
del obj 'Одеяло'
$mur=''
buz=2
goto $lok
-- ОдеялоНаРазбитое окно ---------------------------------

# Укузел
if $dej = 'glaz': 
	pl 'Ну просто прелесть, мой Укузел! Но ведь ему неудобно!'&nl
end
if $dej = 'shup': 
	if bu1<4:
		pl 'Укузя тихонько дотрагивается до плеча Укузела, но он не просыпается.'&nl
		msg 'Хороший мой! Проснись!'
		pl 'Наверное, не стоит его будить, ведь они с Фирелем ночью чинили крышу.'
		pl 'Ладно, пусть спит!'&nl
		nor=1
	end
	if bu1>3:
		pl 'Укузя тормошит Укузела.'&nl
		msg 'Хороший мой! Проснись!'
		wait 1000
		pl 'Укузел просыпается.'&nl
		msg 'С добрым утром!'
		msg 'О! Привет! Как вы там, у Бузюли?'
		msg 'Хорошо! Мы уже позавтракали!'
		msg 'А я?'
		msg 'Идем, мы тебе оставили!'
		nork=1
		pl 'Укузя и Укузел идут завтракать.'&nl
		goto 'busulya'
	end
end
if $dej = 'vzyat':
	pl 'Вряд ли Укузя его поднимет, ведь ее Укузел давно не укузенок!'&nl
end
goto $lok

-- Укузел ---------------------------------

# ВеникНаУкузел
pl 'Э-э-э... А он сдачи не даст?'&nl
goto $lok

-- ВеникНаУкузел ---------------------------------

# ВеникНаУкузята
pl 'Ну они-то чем провинились?'&nl
goto $lok

-- ВеникНаУкузята ---------------------------------

# ВеникНаРазбитое окно
pl 'Окно и так уже разбито!'&nl
goto $lok

-- ВеникНаРазбитое окно ---------------------------------

# Цветочный горшокНаУкузята
pl 'Младший Укузенок уже пострадал от этого горшка, что же теперь, старшего зашибить?'&nl
goto $lok
-- Цветочный горшокНаУкузята ---------------------------------

# dor_omaz
$lok = $curlok
if no obj 'Укузенок':
	pl 'На другой стороне ручья живет бабушка Омаза. Укузя к ней идти не собиралась!'&nl
	goto 'ruchej'
end
if obj 'Укузенок':
	*pl 'ПОКА ЭТО КОНЕЦ'&*nl
	*pl 'ПРОДОЛЖЕНЕ СЛЕДУЕТ'&*nl
	*pl 'ПАРОЛЬ ДЛЯ СЛЕДУЮЩЕЙ ЧАСТИ :'&*nl
	if obj 'Осколок':*pl 'ОМАЗАОС'
	if no obj 'Осколок':*pl 'ОМАЗА'
end 
-- dor_omaz ---------------------------------

# Укузенок
if $dej = 'glaz': 
	pl 'Горе мое луковое!'&nl
end
if $dej = 'shup': 
	pl 'Укузя крепче сжимает лапку Укузенка.'&nl
end
if $dej = 'vzyat':
	$mur = 'Укузенок'
	msg 'Идем-идем! Не отставай!'
end
goto $lok

-- Укузенок ---------------------------------

